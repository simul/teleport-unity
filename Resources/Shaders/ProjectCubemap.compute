
// (c) 2020 Simul.co

#pragma kernel EncodeCameraPositionCS
#pragma kernel DecomposeCS
#pragma kernel DecomposeDepthCS

#include "Common.cginc"

// -1 because Unity uses OpenGL convention where bottom left is 0,0
static const int2 FaceOffsets[] = { {0,0},{1,0},{2,0},{0,1},{1,1},{2,1} };

// The input texture, but as an array of 6 2d faces.
RWTexture2DArray<float4> RWInputCubeAsArray;
RWTexture2D<float4> RWOutputColorTexture;

int2 Offset;
float3 CubemapCameraPositionMetres;

// Here. we will encode the camera position CubemapCameraPositionMetres in blocks of monochrome colour.
[numthreads(32, 4, 1)]
void EncodeCameraPositionCS(uint2 ThreadID : SV_DispatchThreadID)
{
	uint OutputW, OutputH;
	RWOutputColorTexture.GetDimensions(OutputW, OutputH);

	// We want to encode three 32-bit numbers with the minimum possible loss.
	// Therefore we will encode them in...
	// Binary.
	uint3 raw_uint = asuint(CubemapCameraPositionMetres);

	// We will use the thread x as the bit index.
	uint3 masked_bits = (raw_uint >> (ThreadID.x / 4))& uint(1);		// 1 or 0, 1 or 0, 1 or 0
	int2 Pos = Offset;
	Pos.x += int(ThreadID.x);
	Pos.y += int(ThreadID.y);
	float4 X = masked_bits.xxxx;
	float4 Y = masked_bits.yyyy;
	float4 Z = masked_bits.zzzz;
	RWOutputColorTexture[Pos] = X;
	// minus here instead of plus because Unity uses OpenGL convention where bottom left is 0,0
	RWOutputColorTexture[Pos + int2(0, 8)] = Y; 
	RWOutputColorTexture[Pos + int2(0, 16)] = Z;
}

[numthreads(THREADGROUP_SIZEX, THREADGROUP_SIZEY, 1)]
void DecomposeCS(uint3 ThreadID : SV_DispatchThreadID)
{
	uint InputW, InputH, InputD;
	RWInputCubeAsArray.GetDimensions(InputW, InputH, InputD);
	uint OutputW, OutputH;
	RWOutputColorTexture.GetDimensions(OutputW, OutputH);
	int3 pos = int3(ThreadID);

	if (ThreadID.x >= OutputW || ThreadID.y >= OutputH)
		return;

	float4 SceneColor = RWInputCubeAsArray[pos];
	SceneColor.x = sqrt(SceneColor.x);
	SceneColor.y = sqrt(SceneColor.y);
	SceneColor.z = sqrt(SceneColor.z);
	
	RWOutputColorTexture[int2(ThreadID.x, ThreadID.y) + Offset + InputW * FaceOffsets[pos.z]] = SceneColor;
}

float PosToDistanceMultiplier(int2 pos, int w)
{
	float h = (w + 1.0) / 2.0;
	vec2 diff = (vec2(pos) - vec2(h, h)) * 2.0 / vec2(w, w);
	return sqrt(1.0 + dot(diff, diff));
}

float GetDepth(int3 pos, int w)
{
	float m = PosToDistanceMultiplier(pos.xy, w);
	float d = RWInputCubeAsArray[pos].a;
	d *= m;
	return d;
}

[numthreads(THREADGROUP_SIZEX, THREADGROUP_SIZEY, 1)]
void DecomposeDepthCS(uint3 ThreadID : SV_DispatchThreadID)
{
	uint InputW, InputH, InputD;
	RWInputCubeAsArray.GetDimensions(InputW, InputH, InputD);
	uint OutputW, OutputH;
	RWOutputColorTexture.GetDimensions(OutputW, OutputH);

	int3 pos = int3(ThreadID);
	pos.xy *= 2;

	if (ThreadID.x >= OutputW || ThreadID.y >= OutputH)
		return;

	float d00 = GetDepth(pos, InputW);
	float d01 = GetDepth(pos + int3(1, 0, 0), InputW);
	float d10 = GetDepth(pos + int3(0, 1, 0), InputW); 
	float4 DepthValue = float4(d00, d01, d10, 1.0) / 100.0 / 20.0;

	RWOutputColorTexture[int2(ThreadID.x, ThreadID.y) + Offset + InputW * FaceOffsets[pos.z] / 2] = DepthValue;
}

